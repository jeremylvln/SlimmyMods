buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }

    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

configure(subprojects.findAll {
    !['libs', 'mods'].contains(it.name)
}) {
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'org.parchmentmc.librarian.forgegradle'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    group "fr.iamblueslime.slimmymods"

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    configurations {
        implementationBundled
    }

    repositories {
        maven { url "https://dvs1.progwml6.com/files/maven" }
        maven { url "https://cursemaven.com" }
    }

    dependencies {
        minecraft "net.minecraftforge:forge:${forge_version}"

        compileOnly fg.deobf("mezz.jei:jei-${jei_version}:api")
        implementation fg.deobf("mezz.jei:jei-${jei_version}")

        implementation fg.deobf("curse.maven:the-one-probe-245211:${top_version}")
    }

    minecraft {
        mappings channel: 'parchment', version: parchment_version
    }

    sourceSets {
        main {
            compileClasspath += configurations.implementationBundled
            runtimeClasspath += configurations.implementationBundled
        }
    }

    jar {
        from {
            configurations.implementationBundled.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }

    jar.finalizedBy('reobfJar')

    task sourcesJar(type: Jar, dependsOn: [classes]) {
        from sourceSets.main.allJava
        archiveClassifier.set("sources")
        manifest.attributes(jar.manifest.attributes)
    }

    jar.finalizedBy(sourcesJar)

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact jar
            }
        }

        repositories {
            maven {
                url "file://${project.projectDir}/mcmodsrepo"
            }
        }
    }
}
